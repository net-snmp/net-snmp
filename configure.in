dnl Process this file with autoconf to produce a configure script.
AC_INIT(agent/extensible/extensible.c)
AC_CONFIG_HEADER(config.h)

dnl Checks for programs.
AC_CHECK_PROGS(CC,cc gcc c89,cc)
AC_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET
AC_PROG_RANLIB
AC_PATH_PROG(PSPROG,ps)
AC_CHECK_PROGS(SED,sed gsed)

AC_AIX

dnl system check
AC_CANONICAL_SYSTEM
CFLAGS="$CFLAGS -D`echo $target_os | sed 's/\..*//'`"

dnl Solaris specialties
case "$target_os" in
  solaris2*) CPPFLAGS="$CPPFLAGS -I/usr/ucbinclude -I/usr/include" ; LDFLAGS="-L/usr/ucblib -R/usr/ucblib $LDFLAGS" ;;
esac

dnl Set SNMPLIBPATH
if test "x$prefix" = "xNONE"; then
AC_DEFINE_UNQUOTED(SNMPLIBPATH,"$ac_default_prefix/lib/snmp")
else
AC_DEFINE_UNQUOTED(SNMPLIBPATH,"$prefix/lib/snmp")
fi

dnl
dnl Check for kernel location
dnl

AC_CACHE_CHECK(for location of sytem kernel,KERNEL_LOC,
[KERNEL_LOC="unknown"
for i in /vmunix /hp-ux /stand/vmunix /kernel/unix /kernel/genunix /netbsd /unix
  do
  if test -f $i; then
    AC_DEFINE_UNQUOTED(KERNEL_LOC,"$i")
    KERNEL_LOC="$i"
    break;
  fi
done
if test $i = "unknown"; then
  AC_MSG_WARN(Can't find system kernel...  Setting to /vmunix)
  AC_DEFINE(KERNEL_LOC,"/vmunix")
  KERNEL_LOC="/vmunix"
fi
])

dnl Check for /dev/dmem or /dev/drum location
AC_CACHE_CHECK(for location of swap device,DMEM_LOC,
[
dnl First determine if test expects a -f or a -c (character device (SYSV))

if test -f /dev/kmem; then
  CTEST="test -f"
elif test -c /dev/kmem; then
  CTEST="test -c"
else
  dnl fall back
  CTEST="test -f"
fi

if $CTEST /dev/dmem; then
  AC_DEFINE(DMEM_LOC,"/dev/dmem")
  DMEM_LOC="/dev/dmem"
elif $CTEST /dev/drum; then
  AC_DEFINE(DMEM_LOC,"/dev/drum")
  DMEM_LOC="/dev/drum"
else
  DMEM_LOC="none"
fi
])

dnl Checks for libraries.
dnl Replace `main' with a function in -lX11:
dnl AC_CHECK_LIB(X11, main)
dnl Replace `main' with a function in -lXau:
dnl AC_CHECK_LIB(Xau, main)
dnl Replace `main' with a function in -lXaw:
dnl AC_CHECK_LIB(Xaw, main)
dnl Replace `main' with a function in -lXext:
dnl AC_CHECK_LIB(Xext, main)
dnl Replace `main' with a function in -lXmu:
dnl AC_CHECK_LIB(Xmu, main)
dnl Replace `main' with a function in -lXt:
dnl AC_CHECK_LIB(Xt, main)
dnl Replace `main' with a function in -ldes:
dnl AC_CHECK_LIB(des, main)
dnl Replace `main' with a function in -lelf:
AC_CHECK_LIB(elf, main)
dnl Replace `main' with a function in -lm:
AC_CHECK_LIB(m, asin)
dnl Replace `main' with a function in -lnsl:
AC_CHECK_LIB(nsl, gethostbyname)
dnl Replace `main' with a function in -loldX:
dnl AC_CHECK_LIB(oldX, main)
dnl Replace `main' with a function in -lsnmp:
dnl AC_CHECK_LIB(snmp, main)
dnl Replace `main' with a function in -lsocket:
AC_CHECK_LIB(socket, getservbyname)
dnl Replace `main' with a function in -lsocket:
AC_CHECK_LIB(ucb, bzero)

dnl more solaris specialities
dnl if libucb does not define bzero and I am solaris, add -DSVR4 to CFLAGS
case "$target_os" in
  solaris2*) 
  if test "X$HAVE_LIBUCB" = "X"; then
    CFLAGS="$CFLAGS -DSVR4"
  fi ;;
esac


dnl Replace `main' with a function in -ltcl:
dnl AC_CHECK_LIB(tcl, main)
dnl Replace `main' with a function in -ltk:
dnl AC_CHECK_LIB(tk, main)
dnl Replace `main' with a function in -lkstat:
AC_CHECK_LIB(kstat, kstat_lookup)
dnl Replace `main' with a function in -lkvm:
AC_CHECK_LIB(kvm, kvm_physaddr)

dnl Checks for header files.
AC_HEADER_STDC
AC_HEADER_DIRENT
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS(fcntl.h limits.h sys/file.h sys/ioctl.h syslog.h unistd.h netinet/tcpip.h netinet/in.h sys/dmap.h machine/pte.h xti.h sys/sockio.h fstab.h sys/fs.h mtab.h ufs/fs.h sys/fixpoint.h machine/param.h sys/vmmeter.h sys/mbuf.h sys/time.h sys/swap.h inet/mib2.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_TYPE_OFF_T
AC_TYPE_PID_T
AC_HEADER_TIME

dnl checking for 4.3 vs 4.4 rtentry.
AC_CACHE_CHECK(type of rtentry structure, RTENTRY_TYPE,
[

dnl 4.3 compat
AC_TRY_COMPILE([#define KERNEL
#include<net/route.h>
],[
struct rtentry rt; 
rt.rt_hash = 1;
], RTENTRY_TYPE="BSD-4.3")

dnl 4.4 compat
if test "x$RTENTRY_TYPE" = "x"; then
  AC_TRY_COMPILE([#define KERNEL
#include<net/route.h>
],[
  struct rtentry rt; 
  rt.rt_nodes[0].rn_b = 1;
  ], RTENTRY_TYPE="BSD-4.4")
fi

dnl else ack.
if test "x$RTENTRY_TYPE" = "x"; then
  AC_MSG_WARN(Unknown)
  RTENTRY_TYPE="unknown"
fi

])

if test "x$RTENTRY_TYPE" = "xBSD-4.4"; then
  AC_DEFINE(RTENTRY_4_4)
fi

dnl checking for alpha's ortentry vs rtentry
if test "x$RTENTRY_TYPE" = "xunknown"; then
AC_CACHE_CHECK(for struct rtentry, ac_cv_struct_rtentry,
[AC_EGREP_CPP(ortentry, [#define KERNEL
#include<net/route.h>
],  ac_cv_struct_rtentry=ortentry,  ac_cv_struct_rtentry=rtentry )
if test "x$ac_cv_struct_rtentry" = "xrtentry" ; then
  AC_DEFINE(RTENTRY,struct rtentry)
else
  AC_DEFINE(RTENTRY,struct ortentry)
fi
])
else
  RTENTRY_TYPE="rtentry"
  AC_DEFINE(RTENTRY,struct rtentry)
fi


dnl Check ps args
AC_CACHE_CHECK(for correct flags to ps, ac_cv_ps_flags,
[if test "`($PSPROG -e 2>&1) | egrep ' ps$' | awk '{print $NF}'`" = "ps" ; then
  ac_cv_ps_flags="-e"
elif test "`($PSPROG -el 2>&1) | egrep ' ps$' | awk '{print $NF}''`" = "ps" ; then
  ac_cv_ps_flags="-el"
elif test "`($PSPROG -acx 2>&1) | egrep ' ps$' | awk '{print $NF}''`" = "ps" ; then
  ac_cv_ps_flags="-acx"
else
  AC_MSG_WARN(Unable to determine valid ps flags...  defaulting...)
  ac_cv_ps_flags="-acx"
fi
])

AC_DEFINE_UNQUOTED(PSCMD, "$PSPROG $ac_cv_ps_flags")

dnl Checks for byte order
AC_C_BIGENDIAN

dnl Checks for library functions.
AC_FUNC_ALLOCA
AC_PROG_GCC_TRADITIONAL
AC_FUNC_MEMCMP
AC_TYPE_SIGNAL
AC_CHECK_FUNCS(gethostname gettimeofday select socket strtol)

AC_CACHE_CHECK(if you have run configure before, ac_cv_have_warned,
[
cat << EOF

	 ************** Configuration Section **************

	You are about to be prompted by a series of questions.  Answer
them carefully, as they determine how the snmp agent and related
applications are to function.

	After the configure script finishes, you can browse the newly
created config.h file for further - less important - parameters to
modify.  Be careful if you re-run configure though since config.h will
be over written.

-Press return to continue-
EOF

read tmpinput
ac_cv_have_warned="yes"
echo $ac_n "disabling above prompt for future runs...  $ac_c"
])

ME=`whoami`
if test -f /etc/resolv.conf; then
  LOC=`cat /etc/resolv.conf | grep '^domain' | tail -1 | awk '{print $NF}'`
else
  LOC="@no.where"
fi

AC_PROMPT_USER(SYS_CONTACT,[

*** System Contact Information:

	Describes who should be contacted about the host the agent is
running on.  This information is available in the MIB-II tree.

System Contact Information],$ME@$LOC,1)

AC_PROMPT_USER(SYS_LOC,[

*** System Location:

	Describes the location of the system.  This information is
available in the MIB-II tree.

System Location],UCDavis Electrical Engineering Departement,1)

AC_PROMPT_USER(LOGFILE,[

*** Logfile location:

	Enter the default location for the snmpd agent to dump
information & errors to.  If not defined (enter the keyword \"none\"
at the prompt below) the agent will use stdout and stderr instead.
(Note: This value can be over-ridden using the command line)

Location to write logfile],/usr/adm/snmpd.log,1)

AC_CACHE_CHECK(Default Extensible Mib location, ac_cv_user_prompt_EXTENSIBLEMIB,
[
AC_PROMPT_USER_NO_DEFINE(EXTENSIBLEMIB,[

*** Default Extensible Mib Location:

	You need to pick a MIB location that will be the top of the
extensible portion of the agent.  This should be the ASN identity
assigned to your institution.  

	If you don't have one:, make one up or use the default below.  

Default Extensible Mib location],.1.3.6.1.4.10)

ac_cv_user_prompt_EXTENSIBLEDOTMIB=`echo $EXTENSIBLEMIB | sed -e 's/^\.//'`
ac_cv_user_prompt_EXTENSIBLEMIB=`echo $EXTENSIBLEMIB | sed -e 's/^\.//;s/\./,/g'`
echo $ac_n "setting Default Mib location...  $ac_c"

AC_DEFINE_UNQUOTED(EXTENSIBLEMIB,$ac_cv_user_prompt_EXTENSIBLEMIB)
AC_DEFINE_UNQUOTED(EXTENSIBLEDOTMIB,$ac_cv_user_prompt_EXTENSIBLEDOTMIB)
EXTENSIBLECOUNT=`echo $ac_cv_user_prompt_EXTENSIBLEMIB | sed -e 's/\,/ /g' | wc -w`
AC_DEFINE_UNQUOTED(EXTENSIBLENUM,$EXTENSIBLECOUNT)
]) dnl

AC_PROMPT_USER(GLOBALSECURITY,[

*** SNMP Security/Authentication:

	You may pick one of two types of SNMP authentication to use
for this agent.  Only the extensible section is affected by the
following choices.  This will be handled better in version 3.2 and
above.  Note that to use anything other than SNMPV1 the configuration
files must be set up properly for SNMPV2 usage.

	(Encryption is not supported because of U.S. cryptography laws)

SNMPV1       Any type of snmp transmission can access the extensible tree.
SNMPV2ANY    Any type of snmpV2 transmissions can access the extensible tree.
SNMPV2AUTH   Only authenticated V2 transmissions can access the extensible tree.

	If you don't understand any of the above sentences: pick SNMPV1.

What type of security to use],SNMPV2AUTH)

AC_OUTPUT(Makefile snmplib/Makefile snmptcl/Makefile agent/Makefile apps/Makefile apps/snmpnetstat/Makefile agent/extensible/Makefile local/Makefile man/Makefile ov/Makefile)

